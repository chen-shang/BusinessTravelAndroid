plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.business.travel.app"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations {
        all*.exclude module: 'tomcat-annotations-api'
        // This removes all other versions of `support-v4` if gets duplicated from all the artifacts.
    }
    packagingOptions {
        exclude 'META-INF/spring*'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/web-fragment.xml'
        exclude 'META-INF/additional-spring-configuration-metadata.json'
        exclude 'META-INF/notice.txt'
    }

    viewBinding {
        enabled = true
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res/layouts/project',
                           'src/main/res/layouts/my',
                           'src/main/res/layouts/bill',
                           'src/main/res/layouts/item',
                           'src/main/res/layouts/log',
                           'src/main/res/layouts/master',
                           'src/main/res/layouts/base',
                           'src/main/res/layouts/keyboard',
                           'src/main/res/layouts/popup',
                           'src/main/res/layouts/view',
                           'src/main/res/layouts',
                           'src/main/res',
                           'src/main/res/drawables/icon',
                           'src/main/res/drawables/icon/keyboard',
                           'src/main/res/drawables/icon/common',
                           'src/main/res/drawables/icon/project',
                           'src/main/res/drawables/icon/consumption',
                           'src/main/res/drawables/icon/consumption/income',
                           'src/main/res/drawables/icon/consumption/spending',
                           'src/main/res/drawables/icon/member',
                           'src/main/res/drawables/corner',
                           'src/main/res/drawables/shape',
                           'src/main/res/drawables/icon/me',
                           'src/main/res/drawables']
        }
    }
}

dependencies {
    //business.travel 公共
    implementation 'com.business.travel:travel-common:2.1-SNAPSHOT'
    implementation 'com.business.travel:travel-app-common:2.1-SNAPSHOT'
    //第三方类库
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'


    implementation "androidx.room:room-runtime:2.3.0"
    def room_version = "2.3.0"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // optional - RxJava2 support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    // optional - RxJava3 support for Room
    implementation "androidx.room:room-rxjava3:$room_version"
    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"
    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"
    // optional - Paging 3 Integration
    implementation "androidx.room:room-paging:2.4.0-rc01"

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    // aop 支持
    implementation 'org.aspectj:aspectjrt:1.9.7'
    // if u use AndroidX, use the following
    implementation 'com.blankj:utilcodex:1.31.0'

    //圆角图片控件
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    //专治原生控件各种不服控件
    implementation 'com.github.RuffianZhong:RWidgetHelper:androidx.v0.0.8'
    //下拉刷新控件
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    //侧滑拖拽控件
    implementation 'com.yanzhenjie.recyclerview:x:1.3.2'

    //加载图片
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    //加载svg
    implementation 'com.pixplicity.sharp:library:1.1.0'

    implementation 'me.laoyuyu.aria:core:3.8.16'
    annotationProcessor 'me.laoyuyu.aria:compiler:3.8.16'
    //按钮组,这个地方暂时不能升级
    //noinspection GradleDependency
    implementation 'nl.bryanderidder:themed-toggle-button-group:1.3.4'
    implementation 'com.nex3z:toggle-button-group:1.2.3'
    //标题栏
    implementation 'com.github.getActivity:TitleBar:9.2'

    //为了防止不必要的依赖冲突，0.0.3开始需要自行依赖google material库
    implementation 'com.google.android.material:material:1.4.0'
    //查看最新版本请前往github，仅支持androidx
    implementation 'com.github.loperSeven:DateTimePicker:0.1.0'

    //mapstruct 拷贝对象
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    implementation 'org.mapstruct:mapstruct-jdk8:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    //弹框组件
    implementation 'com.github.li-xiaojun:XPopup:2.6.7'

    //分页图标组件
    implementation 'com.github.mtjsoft:GridPager:v3.7.0'

    //动画
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    //搜索列表
    implementation 'com.github.mancj:MaterialSearchBar:0.8.5'

    implementation 'io.github.lapism:search:1.1.0'
}

//编译的时候织入代码
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

final def log = project.logger
final def variants = project.android.applicationVariants

variants.all { variant ->
    // 注意这里控制debug下生效，可以自行控制是否生效
    if (!variant.buildType.isDebuggable()) {
        log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
        return
    }

    JavaCompile javaCompile = variant.javaCompileProvider.get()
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo", "-1.8",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)
        for (IMessage message in handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break
                case IMessage.WARNING:
                    log.warn message.message, message.thrown
                    break
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break
            }
        }
    }
}